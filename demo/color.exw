--
-- color.exw
-- =========
--
-- Test terminal colours
--
without js -- position(), wait_key()
--/*
-- Ensure this is compatible with RDS Eu 2.4:
include ..\builtins\graphics.e
include ..\builtins\get.e
include ..\builtins\wildcard.e
--*/
                                                -- WIN  LNX
constant colour_names = {"BLACK",               --  0    0=
                         "BLUE",                --  1    4
                         "GREEN",               --  2    2=
                         "CYAN",                --  3    6
                         "RED",                 --  4    1
                         "MAGENTA",             --  5    5=
                         "BROWN",               --  6    3
                         "WHITE",               --  7    7=
                         "GRAY",                --  8    8=
                         "BRIGHT_BLUE",         --  9    12
                         "BRIGHT_GREEN",        -- 10    10=
                         "BRIGHT_CYAN",         -- 11    14
                         "BRIGHT_RED",          -- 12    9
                         "BRIGHT_MAGENTA",      -- 13    13=
                         "YELLOW",              -- 14    11
                         "BRIGHT_WHITE"}        -- 15    15=
constant colour_values = {BLACK,
                          BLUE,
                          GREEN,
                          CYAN,
                          RED,
                          MAGENTA,
                          BROWN,
                          WHITE,
                          GRAY,
                          BRIGHT_BLUE,
                          BRIGHT_GREEN,
                          BRIGHT_CYAN,
                          BRIGHT_RED,
                          BRIGHT_MAGENTA,
                          YELLOW,
                          BRIGHT_WHITE}

integer key, fgrnd, bgrnd
    fgrnd = 0
    bgrnd = 0

    while 1 do
        text_color(WHITE)
        bk_color(BLACK)

        clear_screen()
        position(1, 1)
        puts(1, "This tests the screen colour capabilities of the terminal\nPress ...\n")
        puts(1, "   F  -> next foreground colour\n")
        puts(1, "   B  -> next background colour\n")
        puts(1, "   Q or Esc -> quits\n")

        position(7, 1)
        printf(1, "%-10s %-20s %-10s\n", {"", "Name", "Value"})
        printf(1, "%-10s %-20s %-10d\n", {"Fore", colour_names[find(fgrnd,colour_values)], fgrnd})
        printf(1, "%-10s %-20s %-10d\n", {"Back", colour_names[find(bgrnd,colour_values)], bgrnd})
        position(12, 1)

        text_color(fgrnd)
        bk_color(bgrnd)

        printf(1, " +-----------------------------------------------+ \n")
        printf(1, " | This is an example of what it would look like | \n")
        printf(1, " +-----------------------------------------------+ \n")

        key = wait_key()
        if key=27 then exit end if  -- escape
        key = upper(key)
        if key='Q' then exit end if

        if key='B' then
            bgrnd += 1
            if bgrnd=16 then bgrnd = 0 end if
        elsif key='F' then
            fgrnd += 1
            if fgrnd=16 then fgrnd = 0 end if
        end if

    end while
    text_color(WHITE)
    bk_color(BLACK)
    clear_screen()
--  free_console()

