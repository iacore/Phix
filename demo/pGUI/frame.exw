--
-- frame.exw
-- =========
--  IupFrame with a label, filler, and button.
--  Note that "FGCOLOR" is added to the frame but also inherited by the button.
--   (though obviously it does not propagate up to the dialog title)
--
--  You make like to remove the IupFill() and see the difference that makes,
--   and/or change the sizes, margins, etc.
--

include pGUI.e

function quit_cb(Ihandle /*ih*/)
  return IUP_CLOSE
end function

constant attrtext = "TITLE=\"This is the frame\", SIZE=160x100, "&
                    "MARGIN=40x20, FGCOLOR=\"0 128 128\"",
         labeltext = "IupFrame Attributes:\n"&substitute(attrtext,", ","\n")

procedure main()
    IupOpen()

    Ihandle quit_bt = IupButton("Quit", "ACTION", Icallback("quit_cb"))
    Ihandle dialog = IupDialog(IupFrame(IupVbox({IupLabel(labeltext),
                                                 IupFill(),
                                                 quit_bt}),
                                        attrtext),
                               "TITLE=IupFrame,MARGIN=10x10")

    IupSetAttributeHandle(dialog, "DEFAULTESC", quit_bt)

    IupShow(dialog)   /* Shows dialog in the center of the screen */
    IupMainLoop()     /* Initializes IUP main loop */
    IupClose()        /* Finishes IUP */
end procedure

main()
