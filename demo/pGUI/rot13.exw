--
-- rot13.exw
--
-- This file is part of Phix. Originally copied from EuIup.
--
with javascript_semantics   -- some layout/spacing issues remain, no IupHelp(as in launch browser), IupFileDlg(doh/erm), etc.
include pGUI.e
include builtins\read_file.e    -- (somewhat unexpectedly p2js-safe, just so long as you don't actually call it!)

function rot13(string s)
    for i=1 to length(s) do
        integer ch = s[i]
        if ch>='a' and ch<='z' then
            s[i] = remainder(ch-('a'-13),26)+'a'
        elsif ch>='A' and ch<='Z' then
            s[i] = remainder(ch-('A'-13),26)+'A'
        end if
    end for
    return s
end function

Ihandle dlg, hText

function OnLoad(atom /*pHandle*/)
    if platform()!=JS then  -- (then again, see pwa/js/poc/drop.html and/or IupCodeText.exw...?)
        Ihandln hDlg = IupFileDlg()
        IupSetAttributes(hDlg, "ALLOWNEW=NO,DIALOGTYPE=OPEN,TITLE=Open")
        IupSetAttributeHandle(hDlg, "PARENTDIALOG", dlg)
        IupPopup(hDlg, IUP_CENTER, IUP_CENTER)
        if IupGetInt(hDlg, "STATUS")= -1 then
            hDlg = IupDestroy(hDlg) -- (no help)
--          return IUP_IGNORE
            return IUP_DEFAULT  -- PL 26/2: IUP_IGNORE effectively disabled the Load button (but not the menu entry)
        end if
        sequence data = read_file(IupGetAttribute(hDlg, "VALUE"))
        IupStoreAttribute(hText, "VALUE", data)
        hDlg = IupDestroy(hDlg)
    end if
--  return IUP_IGNORE
    return IUP_DEFAULT
end function

function OnRotate(atom pHandle)
    if pHandle then end if
    IupStoreAttribute(hText, "VALUE", rot13(IupGetAttribute(hText, "VALUE")))
    return IUP_DEFAULT
end function

function OnQuit(Ihandle /*ih*/)
    return IUP_CLOSE
end function

function OnAboutPhix(Ihandle /*ih*/)
    if platform()!=JS then
        {} = IupHelp("http://phix.x10.mx")
    end if
    return IUP_DEFAULT
end function

function OnAboutIup(Ihandle /*ih*/)
    if platform()!=JS then
        {} = IupHelp("http://www.tecgraf.puc-rio.br/iup/")
    end if
    return IUP_DEFAULT
end function

function OnAboutRot13(Ihandle /*ih*/)
    IupMessage("About", "A simple example showing off Iup for Phix")
    return IUP_DEFAULT
end function

IupOpen()

Ihandle hMenu = IupMenu({IupSubmenu("File",
                                    IupMenu({IupMenuItem("Load File", Icallback("OnLoad")),
                                             IupMenuItem("Rotate", Icallback("OnRotate")),
                                             IupMenuItem("Quit", Icallback("OnQuit"))
                                            })
                                   ),
                         IupSubmenu("Help",
                                    IupMenu({IupMenuItem("About Phix", Icallback("OnAboutPhix")),
                                             IupMenuItem("About Iup", Icallback("OnAboutIup")),
                                             IupMenuItem("About Rot13", Icallback("OnAboutRot13"))
                                            })
                                   )
                        })

--hText = IupText()
--IupSetAttributes(hText, "MULTILINE=YES,EXPAND=YES,WORDWRAP=YES,SIZE=250x100,SCROLLBAR=YES")
hText = IupText("MULTILINE=YES,EXPAND=YES,WORDWRAP=YES,SIZE=250x100,SCROLLBAR=YES")

Ihandle hMainBox = IupVbox({
                            IupLabel("Text to be rotated:"),
                            hText,
                            IupHbox({IupButton("Load File",Icallback("OnLoad")),
                                     IupButton("Rotate",Icallback("OnRotate")),
                                     IupButton("Quit",Icallback("OnQuit"))
                                    })
                           }, "GAP=5,MARGIN=5x5")

dlg = IupDialog(hMainBox)
IupStoreAttribute(dlg, "TITLE", "Rot 13")
IupSetAttributeHandle(dlg, "MENU", hMenu)
IupShow(dlg)
IupSetAttributeHandle(NULL, "PARENTDIALOG", dlg)
if platform()!=JS then
    IupMainLoop()
    IupClose()
end if
