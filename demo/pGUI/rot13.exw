--
-- rot13.exw
--
-- This file is part of Phix. Originally copied from EuIup.
--

include pGUI.e
include builtins\read_file.e

function esc_close(Ihandle /*ih*/, atom c)
-- (I like all my demos to close when escape is keyed)
    return iff(c=K_ESC?IUP_CLOSE:IUP_CONTINUE)
end function

function rot13(string s)
integer ch
    for i=1 to length(s) do
        ch = s[i]
        if ch>='a' and ch<='z' then
            s[i] = remainder(ch-('a'-13),26)+'a'
        elsif ch>='A' and ch<='Z' then
            s[i] = remainder(ch-('A'-13),26)+'A'
        end if
    end for
    return s
end function

Ihandle hDialog, hText

function OnLoad(atom /*pHandle*/)
sequence data
Ihandle hDlg = IupFileDlg()
    IupSetAttributes(hDlg, "ALLOWNEW=NO,DIALOGTYPE=Open,TITLE=Open")
    IupSetAttributeHandle(hDlg, "PARENTDIALOG", hDialog)
    IupPopup(hDlg, IUP_CENTER, IUP_CENTER)
    if IupGetInt(hDlg, "STATUS")= -1 then
        return IUP_IGNORE
    end if
    data = read_file(IupGetAttribute(hDlg, "VALUE"))
    IupStoreAttribute(hText, "VALUE", data)
    IupDestroy(hDlg)
    return IUP_IGNORE
end function

function OnRotate(atom pHandle)
    if pHandle then end if
    IupStoreAttribute(hText, "VALUE", rot13(IupGetAttribute(hText, "VALUE")))
    return IUP_DEFAULT
end function

function OnQuit(Ihandle ih)
    if ih then end if
    return IUP_CLOSE
end function

function OnAboutPhix(Ihandle ih)
    if ih then end if
    {} = IupHelp("http://phix.is-great.org")
    return IUP_DEFAULT
end function

function OnAboutIup(Ihandle ih)
    if ih then end if
    {} = IupHelp("http://www.tecgraf.puc-rio.br/iup/")
    return IUP_DEFAULT
end function

function OnAboutRot13(Ihandle ih)
    if ih then end if
    IupMessage("About", "A simple example showing off Iup for Phix")
    return IUP_DEFAULT
end function

IupOpen()

Ihandle hMenu = IupMenu({
                         IupSubmenu("File",
        IupMenu({
                 IupMenuItem("Load File",NULL, Icallback("OnLoad")),
                 IupMenuItem("Rotate",NULL, Icallback("OnRotate")),
                 IupMenuItem("Quit",NULL, Icallback("OnQuit"))
        })
                                   ),
                         IupSubmenu("Help",
        IupMenu({
                 IupMenuItem("About Phix",NULL, Icallback("OnAboutPhix")),
                 IupMenuItem("About Iup",NULL, Icallback("OnAboutIup")),
                 IupMenuItem("About Rot13",NULL, Icallback("OnAboutRot13"))
        })
                                   )
                        })

hText = IupText()
IupSetAttributes(hText, "MULTILINE=YES,EXPAND=YES,WORDWRAP=YES,SIZE=250x100,SCROLLBAR=YES")

Ihandle hMainBox = IupVbox({
                            IupLabel("Text to be rotated:"),
                            hText,
                            IupHbox({
        IupButton("Load File",NULL, Icallback("OnLoad")),
        IupButton("Rotate",NULL, Icallback("OnRotate")),
        IupButton("Quit",NULL, Icallback("OnQuit"))
                                    })
                           }, "GAP=5,MARGIN=5x5")

hDialog = IupDialog(hMainBox)
IupStoreAttribute(hDialog, "TITLE", "Rot 13")
IupSetAttributeHandle(hDialog, "MENU", hMenu)
IupSetCallback(hDialog, "K_ANY", Icallback("esc_close"));
IupShow(hDialog)

IupMainLoop()
IupClose()
