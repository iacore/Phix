--
-- demo\pGUI\triangle.exw
-- ======================
--
--  Translation of http://rosettacode.org/wiki/OpenGL#Lua
--
--  Converted to pGUI by Pete Lomax
--  desktop/Phix only: demo/rosetta/OpenGL.exw is a p2js-compatible version
--
--{{trans|Lua}}
-- 02/10/21:
--  tried adding "(script src="js/poc/glsim.js")(/script) after opengl.js
--  added "let gls = new GLSim(canvas);" after canvas had been created (in test.htm).
--  ==> stuck on glShadeModel() [undefined]
--with javascript_semantics
without javascript_semantics    -- (not OpenGL ES 2.0 compatible)
include pGUI.e
include opengl.e

function action(Ihandle /*ih*/)

    glClearColor(0.3,0.3,0.3,0.0)
    glClear(GL_COLOR_BUFFER_BIT+GL_DEPTH_BUFFER_BIT)

    glShadeModel(GL_SMOOTH)

    glLoadIdentity()
    glTranslate(-15.0, -15.0, 0.0)

    glBegin(GL_TRIANGLES)
    glColor(1.0, 0.0, 0.0)
    glVertex(0.0, 0.0)
    glColor(0.0, 1.0, 0.0)
    glVertex(30.0, 0.0)
    glColor(0.0, 0.0, 1.0)
    glVertex(0.0, 30.0)
    glEnd()

    glFlush()

    return IUP_DEFAULT
end function

function resize_cb(Ihandle /*ih*/, integer width, integer height)
    glViewport(0, 0, width, height)
    glMatrixMode(GL_PROJECTION)
    glLoadIdentity()
    glOrtho(-30.0, 30.0, -30.0, 30.0, -30.0, 30.0)
    glMatrixMode(GL_MODELVIEW)
    return IUP_DEFAULT
end function

Ihandln dialog
Ihandle canvas

function map_cb(Ihandle /*ih*/)
    IupGLMakeCurrent(canvas)
    integer {width, height} = IupGetIntInt(dialog, "RASTERSIZE")
    return resize_cb(dialog, width, height)
end function

procedure main()
    IupOpen()
    IupGLCanvasOpen()

    canvas = IupGLCanvas(Icallback("action"), "RASTERSIZE=640x480")
    IupSetCallback(canvas, "RESIZE_CB", Icallback("resize_cb"))

    dialog = IupDialog(canvas, "TITLE=Triangle, SHRINK=YES")
    IupSetCallback(dialog, "MAP_CB", Icallback("map_cb"))

    IupShow(dialog)
--  if platform()!=JS then  -- (no chance, see demo/rosetta/OpenGL.exw)
        IupMainLoop()
        dialog = IupDestroy(dialog)
        IupClose()
--  end if
end procedure
main()

