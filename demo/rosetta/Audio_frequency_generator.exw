--
-- demo\rosetta\Audio_frequency_generator.exw
--
with javascript_semantics
include pGUI.e
Ihandle frequency, duration
include builtins\beep.e

function button_cb(Ihandle /*playbtn*/)
    beep(IupGetInt(frequency,"VALUE"),IupGetInt(duration,"VALUE"))
    return IUP_DEFAULT
end function

function valuechanged_cb(Ihandle val)
    -- (maintain the labels as the sliders are moved)
    IupSetInt(IupGetBrother(val, true),"TITLE",IupGetInt(val,"VALUE"))
    return IUP_DEFAULT
end function

IupOpen()
frequency = IupValuator("HORIZONTAL", "VALUECHANGED_CB", Icallback("valuechanged_cb"),
                        "EXPAND=HORIZONTAL, CANFOCUS=NO, MIN=50, MAX=10000, VALUE=2000")
duration = IupValuator("HORIZONTAL", "VALUECHANGED_CB", Icallback("valuechanged_cb"),
                       "EXPAND=HORIZONTAL, CANFOCUS=NO, MIN=100, MAX=3000, VALUE=500")
Ihandle flabel = IupLabel("2000","ALIGNMENT=ARIGHT, SIZE=20x8"),
        frame1 = IupFrame(IupHbox({flabel,frequency}),`TITLE="Frequency (Hz): "`),
        dlabel = IupLabel("500","ALIGNMENT=ARIGHT, SIZE=20x8"),
        frame2 = IupFrame(IupHbox({dlabel,duration}),`TITLE="Duration (ms): "`),
        playbtn = IupButton("Play",Icallback("button_cb"),"PADDING=30x0"),
        hbox = IupHbox({IupFill(),playbtn,IupFill()},"MARGIN=0x20"),
        vbox = IupVbox({frame1,frame2,hbox}, "MARGIN=10x5, GAP=5"),
        dlg = IupDialog(vbox,`TITLE="Audio Frequency Generator",RASTERSIZE=500x230`)
--      dlg = IupDialog(vbox,`TITLE="Audio Frequency Generator",RASTERSIZE=500x250`)
--      dlg = IupDialog(vbox,`TITLE="Audio Frequency Generator"`)
IupShow(dlg)
if platform()!=JS then
    IupMainLoop()
    IupClose()
end if

