--
-- demo\rosetta\Fake_Redirection.exw
-- =================================
--
-- for https://rosettacode.org/wiki/User_defined_pipe_and_redirection_operators
--
--{{trans|Go}}
--You could of course do things more character-by-character or line-by-line, and/or farm things out to separate
--threads/tasks, but the latter would need some suspend/resume/scheduling, along with explicit eof markers.
--The distributed version also has a couple of alternatives for pipe_head() and pipe_tail(), along with a 
--class-less version that is compatible with pwa/p2js.
--
--/!*
without js -- see version below

constant fs = new_dict()    -- fake file system

class pipe                  -- fake pipe
  string data = ""
  integer idx = 0
  function getch()
    if idx<length(data) then
        idx += 1
        return data[idx] 
    end if
    return -1
  end function
  function getln()
    if idx<length(data) then
        integer start = idx+1
        idx = find('\n',data,start)
        return data[start..idx]
    end if
    return -1
  end function
  procedure putch(integer ch)
    data &= ch
  end procedure
  procedure putln(string line)
    data &= line
  end procedure
  function readall()
    return split(data,'\n',no_empty:=true)
  end function
  function rawdata()
    return data
  end function
end class

function joinup(sequence lines)
    return join(lines,"\n")&"\n"
end function

procedure toName(string name, pipe src) -- role of > operator
    setd(name,src.rawdata(),fs)
end procedure
 
function fromName(string name)          -- role of < operator
    return new(pipe,{getd(name,fs)})
end function
 
function tee(pipe pin, string name)
    string data = pin.rawdata()
    setd(name,data,fs)
    return new(pipe,{data})
end function
 
function grep(pipe pin, string pat)
    pipe res = new()
    while true do
        object line = pin.getln()
        if atom(line) then exit end if
        if match(pat,line) then
            res.putln(line)
        end if
    end while
    return res
end function

function multireader(sequence pipes)
    pipe res = new()
    for i=1 to length(pipes) do
        pipe p = pipes[i]
        res.putln(p.rawdata())
    end for
    return res
end function

function pipe_head(pipe pin, integer lines)
    pipe res = new()
    for i=1 to lines do
        object line = pin.getln()
        if atom(line) then exit end if
        res.putln(line)
    end for
    return res
-- or, nicer/neater but potentially much wasted effort:
--  return new(pipe,{joinup(head(pin.readall(),lines))})
end function

function pipe_tail(pipe pin, integer lines)
--  sequence ring = repeat("",lines)
--  integer rn = 0, full = false
--  while true do
--      object line = pin.getln()
--      if atom(line) then exit end if
--      rn += 1
--      if rn>lines then {rn,full} = {1,true} end if
--      ring[rn] = line
--  end while
--  ring = iff(full?ring[rn+1..$]:{}) & ring[1..rn]
--  return new(pipe,{joinup(ring)})
    return new(pipe,{joinup(tail(pin.readall(),lines))})
end function

function sort_unique(pipe pin)
    return new(pipe,{joinup(unique(pin.readall()))})
end function
 
procedure showCount(string heading, name)
    if getd_index(name,fs)=NULL then crash("not found") end if
    integer n = length(split(getd(name,fs), "\n"))
    printf(1,"%s: %d\n", {heading, n})
end procedure

constant lcs_txt = """
Wil van der Aalst        business process management, process mining, Petri nets
Hal Abelson              intersection of computing and teaching
Serge Abiteboul          database theory
Samson Abramsky          game semantics
Leonard Adleman          RSA, DNA computing
Manindra Agrawal         polynomial-time primality testing
Luis von Ahn             human-based computation
Alfred Aho               compilers book, the 'a' in AWK
Stephen R. Bourne        Bourne shell, portable ALGOL 68C compiler
Kees Koster              ALGOL 68
Lambert Meertens         ALGOL 68, ABC (programming language)
Peter Naur               BNF, ALGOL 60
Guido van Rossum         Python (programming language)
Adriaan van Wijngaarden  Dutch pioneer; ARRA, ALGOL
Dennis E. Wisnosky       Integrated Computer-Aided Manufacturing (ICAM), IDEF
Stephen Wolfram          Mathematica
William Wulf             compilers
Edward Yourdon           Structured Systems Analysis and Design Method
Lotfi Zadeh              fuzzy logic
Arif Zaman               Pseudo-random number generator
Albert Zomaya            Australian pioneer of scheduling in parallel and distributed systems
Konrad Zuse              German pioneer of hardware and software
""",
mainlist = "List_of_computer_scientists.lst"
setd(mainlist,lcs_txt,fs)
toName("aa", grep(tee(sort_unique(multireader({pipe_head(fromName(mainlist), 4),
                                               tee(grep(fromName(mainlist), "ALGOL"),
                                                   "ALGOL_pioneers.lst"),
                                               pipe_tail(fromName(mainlist), 4)})),
                      "the_important_scientists.lst"),
                  "aa"))
 
printf(1,"Pioneer: %s", getd("aa",fs))
showCount("Number of ALGOL pioneers", "ALGOL_pioneers.lst")
showCount("Number of scientists", "the_important_scientists.lst")

?"done"
{} = wait_key()
abort(0)
--*!/

-- class-less version, compatible with pwa/p2js:
with javascript_semantics

constant fs = new_dict()    -- fake file system

enum DATA, IDX
type pipe(object p)
    return sequence(p) and length(p)=2 and string(p[DATA]) and integer(p[IDX])
end type

function new_pipe(string s="")
    pipe p = {s,0}
    return p
end function

function getch(pipe p)
    if p[IDX]<length(p[DATA]) then
        p = deep_copy(p)
        p[IDX] += 1
        return {p,p[DATA][p[IDX]]}
    end if
    return {p,-1}
end function

function getln(pipe p)
    if p[IDX]<length(p[DATA]) then
        p = deep_copy(p)
        integer start = p[IDX]+1
        p[IDX] = find('\n',p[DATA],start)
        return {p,p[DATA][start..p[IDX]]}
    end if
    return {p,-1}
end function

procedure putch(pipe p, integer ch)
    p[DATA] &= ch
end procedure

function putln(pipe p, string line)
    p[DATA] &= line
    return p
end function

function readall(pipe p)
    return split(p[DATA],'\n')
end function

function rawdata(pipe p)
    return p[DATA]
end function

function joinup(sequence lines)
    return join(lines,"\n")&"\n"
end function

procedure toName(string name, pipe src) -- role of > operator
    setd(name,rawdata(src),fs)
end procedure
 
function fromName(string name)          -- role of < operator
    pipe p = {getd(name,fs),0}
    return p
end function
 
function tee(pipe pin, string name)
    string data = rawdata(pin)
    setd(name,data,fs)
    pipe p = {data,0}
    return p
end function
 
function grep(pipe pin, string pat)
    pipe res = new_pipe()
    while true do
        object line
        {pin, line} = getln(pin)
        if atom(line) then exit end if
        if match(pat,line) then
            res = putln(res,line)
        end if
    end while
    return res
end function

function multireader(sequence pipes)
    pipe res = new_pipe()
    for i=1 to length(pipes) do
        pipe p = pipes[i]
        res = putln(res,rawdata(p))
    end for
    return res
end function

function pipe_head(pipe pin, integer lines)
    pipe res = new_pipe()
    for i=1 to lines do
        object line
        {pin, line} = getln(pin)
        if atom(line) then exit end if
        res = putln(res,line)
    end for
    return res
-- or, nicer/neater but potentially much wasted effort:
--  return new_pipe(joinup(head(readall(pin),lines)))
end function

function pipe_tail(pipe pin, integer lines)
--  sequence ring = repeat("",lines)
--  integer rn = 0, full = false
--  while true do
--      object line
--      {pin, line} = getln(pin)
--      if atom(line) then exit end if
--      rn += 1
--      if rn>lines then {rn,full} = {1,true} end if
--      ring[rn] = line
--  end while
--  ring = iff(full?ring[rn+1..$]:{}) & ring[1..rn]
--  return new_pipe(joinup(ring))
    return new_pipe(joinup(tail(readall(pin),lines)))
end function

function sort_unique(pipe pin)
    return new_pipe(joinup(unique(readall(pin))))
end function
 
procedure showCount(string heading, name)
    if getd_index(name,fs)=NULL then crash("not found") end if
    integer n = length(split(getd(name,fs), "\n"))
    printf(1,"%s: %d\n", {heading, n})
end procedure

constant lcs_txt = """
Wil van der Aalst        business process management, process mining, Petri nets
Hal Abelson              intersection of computing and teaching
Serge Abiteboul          database theory
Samson Abramsky          game semantics
Leonard Adleman          RSA, DNA computing
Manindra Agrawal         polynomial-time primality testing
Luis von Ahn             human-based computation
Alfred Aho               compilers book, the 'a' in AWK
Stephen R. Bourne        Bourne shell, portable ALGOL 68C compiler
Kees Koster              ALGOL 68
Lambert Meertens         ALGOL 68, ABC (programming language)
Peter Naur               BNF, ALGOL 60
Guido van Rossum         Python (programming language)
Adriaan van Wijngaarden  Dutch pioneer; ARRA, ALGOL
Dennis E. Wisnosky       Integrated Computer-Aided Manufacturing (ICAM), IDEF
Stephen Wolfram          Mathematica
William Wulf             compilers
Edward Yourdon           Structured Systems Analysis and Design Method
Lotfi Zadeh              fuzzy logic
Arif Zaman               Pseudo-random number generator
Albert Zomaya            Australian pioneer of scheduling in parallel and distributed systems
Konrad Zuse              German pioneer of hardware and software
""",
mainlist = "List_of_computer_scientists.lst"
setd(mainlist,lcs_txt,fs)
toName("aa", grep(tee(sort_unique(multireader({pipe_head(fromName(mainlist), 4),
                                               tee(grep(fromName(mainlist), "ALGOL"),
                                                   "ALGOL_pioneers.lst"),
                                               pipe_tail(fromName(mainlist), 4)})),
                      "the_important_scientists.lst"),
                  "aa"))
 
printf(1,"Pioneer: %s", getd("aa",fs))
showCount("Number of ALGOL pioneers", "ALGOL_pioneers.lst")
showCount("Number of scientists", "the_important_scientists.lst")

if platform()!=JS then
    ?"done"
    {} = wait_key()
    abort(0)
end if

--/*
Output:
Pioneer: Adriaan van Wijngaarden  Dutch pioneer; ARRA, ALGOL
Number of ALGOL pioneers: 5
Number of scientists: 13
--*/

