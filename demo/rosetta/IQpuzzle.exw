--
-- demo\rosetta\IQpuzzle.exw
-- =========================
--
--  Twee brute-force string-based solution. 
--  Backtracks a mere 366 times, whereas starting with the 5th peg missing backtracks 19388 times.
--
with javascript_semantics
--/!*
function solve(string board, integer left)
    if left=1 then return "" end if
    for i=1 to length(board) do
        if board[i]='1' then
            for mj in {-11,-9,2,11,9,-2} do
                integer over = i+mj, tgt = i+2*mj
                if tgt>=1 and tgt<=length(board) 
                and board[tgt]='0' and board[over]='1' then
                    board = reinstate(board,{i,over,tgt},"001")
                    string res = solve(board,left-1)
                    if length(res)!=4 then return board&res end if
                    board = reinstate(board,{i,over,tgt},"110")
                end if
            end for
        end if
    end for
    return "oops"
end function
 
sequence start = """
----0----
---1-1---
--1-1-1--
-1-1-1-1-
1-1-1-1-1
"""
puts(1,substitute(join_by(split(start&solve(start,14),'\n'),5,7),"-"," "))
--*!/

--/*
--Adapted to the English game:
function solveE(string board, integer left)
    if left=1 then
--      return ""   -- (leaves it on the edge)
        if board[3*15+8]='.' then return "" end if
        return "oops"
    end if
    for i=1 to length(board) do
        if board[i]='.' then
            for mj in {-2,15,2,-15} do
                integer over = i+mj, tgt = i+2*mj
                if tgt>=1 and tgt<=length(board) 
                and board[tgt]='o' and board[over]='.' then
                    board = reinstate(board,{i,over,tgt},"oo.")
                    string res = solveE(board,left-1)
                    if length(res)!=4 then return board&res end if
                    board = reinstate(board,{i,over,tgt},"..o")
                end if
            end for
        end if
    end for
    return "oops"
end function
 
string estart = """
-----.-.-.----
-----.-.-.----
-.-.-.-.-.-.-.
-.-.-.-o-.-.-.
-.-.-.-.-.-.-.
-----.-.-.----
-----.-.-.----
"""
puts(1,substitute(join_by(split(estart&solveE(estart,32),'\n'),7,8),"-"," "))
--*/

{} = wait_key()
