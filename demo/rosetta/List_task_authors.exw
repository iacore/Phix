--
-- demo\rosetta\List_task_authors.exw
-- ==================================
--
--  To keep the output nice and short, lists the top 5 task creators.
--  Uses a cache: once a .hist file has been downloaded for a given
--   task, it is assumed to be good forever. Each task is about 20K,
--   so it will download around 25MB in total, for >= 1,219 tasks.
--   While initially very slow, once cached it is pretty fast (~2s)
--
without js -- (libcurl, file i/o, peek, progress..)
include rosettacode_cache.e -- see [[Rosetta_Code/Count_examples#Phix]]

constant history_user = `<span class='history-user'><a href="`,
         history_span = `<span class='history-user'><span `

function count_tasks()
    sequence tasks = sort(get_rc_category("Programming_Tasks")&
                          get_rc_category("Draft_Programming_Tasks"))
    integer ntasks = length(tasks), e
    progress("%d tasks found\n",{ntasks}) -- 1592 (correct as of 26/12/22)
    sequence users = {},
             utask = {},
             ntask = {}
    atom t1 = time()+1
    for i=1 to ntasks do
        string ti = tasks[i], contents = get_rc_hist(ti,i,ntasks)
--1/3/24 (simplify debugging)
integer hus = match(`<span class='history-user'>`,contents),
        hue = match(`</span>`,contents,hus)
if hus=0 or hue = 0 then ?9/0 end if
contents = contents[hus..hue+6]
        ti = file_clean(ti) -- (in case you want to show them)
        if time()>t1 then
            progress("processing %d of %d - %s\r",{i,ntasks,ti})
            t1 = time()+1
        end if
-- look for and extract "name" from the appropriate <a href="..">name</a>, eg:
-- <span class='history-user'><a href="/mw/index.php?title=User:Daxim&amp;action=edit&amp;redlink=1" class="new mw-userlink" title="User:Daxim (page does not exist)">Daxim</a>
-- <span class='history-user'><a href="/wiki/User:Paddy3118" title="User:Paddy3118" class="mw-userlink">Paddy3118</a>
        integer k = match(history_user,contents)
-- 1/3/24: old:  <span class='history-user'><a href="/mw/index.php?title=User...>Daxim</a> ...
--                                                                               ^^^^^  
--         new: ‎ <span class='history-user'><span...><bdi>imported&gt;CosmiaNebula</bdi></span...
--                                                   ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
--      if k=0 then ?9/0 end if
        if k=0 then
            k = match(history_span,contents)
--<span class='history-user'><span data-mw-revid="360768" class="mw-userlink mw-extuserlink mw-anonuserlink"><bdi>imported&gt;CosmiaNebula</bdi></span></span>‎ <span class="mw-changeslist-separator"></span> <span class="history-size mw-diff-bytes" data-mw-bytes="1446">1,446 bytes</span> <strong dir="ltr" class="mw-plusminus-pos mw-diff-bytes" title="1,446 bytes after change">+1,446</strong>‎ <span class="mw-changeslist-separator"></span>  <span class="comment comment--without-parentheses">creating page</span></li>
--  contents[1..108] = `<span class='history-user'><span data-mw-revid="360703" class="mw-userlink mw-extuserlink mw-anonuserlink"><`
--  contents[109..143] = `bdi>imported&gt;CosmiaNebula</bdi><`
            if k=0 then ?9/0 end if
            k = find('>',contents,k+length(history_span))
            if k=0 then ?9/0 end if
            k += 1
            e = match("</span>",contents,k)
        else
            k = find('>',contents,k+length(history_user))
            if k=0 then ?9/0 end if
            k += 1
            e = match("</a>",contents,k)
        end if
        if e=0 then ?9/0 end if
        string user = contents[k..e-1]
        --  user = "<bdi>[rosettacode&gt;]Mathaddict</bdi>"
        -- (bdi is the html "bidirectional isolation" tag)
        if match("<bdi>rosettacode&gt;",user)=1 then
            assert(user[-6..-1] = "</bdi>")
            user = user[length("<bdi>rosettacode&gt;")+1..-7]
        elsif match("<bdi>",user)=1 then
            assert(user[-6..-1] = "</bdi>")
            user = user[length("<bdi>")+1..-7]
        end if
        k = find(user,users)
        if k=0 then
            users = append(users,user)
            utask = append(utask,{i})
            ntask = append(ntask,1)
        else
            utask[k] &= i
            ntask[k] += 1
        end if
        tasks[i] = ti
        if get_key()=#1B then progress("escape keyed\n") exit end if
    end for
    curl_cleanup()
    progress("\n")
    integer nusers = length(users)
    sequence tags = custom_sort(ntask,tagset(nusers))
    integer top5 = 0
    for i=nusers to 1 by -1 do
        integer ui = tags[i]
        if ntask[ui]<5 then exit end if
        printf(1,"%s tasks:%d\n",{users[ui],ntask[ui]})
        top5 += 1 if top5>5 then exit end if
    end for
    return {ntasks,nusers}
end function

progress("Total: %d tasks by %d authors\n",count_tasks())

?"done"
{} = wait_key()
--/*
-- Output as of 06/2/20:
1219 tasks found

Paddy3118 tasks:176
Markhobley tasks:71
Gerard Schildberger tasks:59
Mwn3d tasks:55
NevilleDNZ tasks:39
Short Circuit tasks:33
Total: 1219 tasks by 280 authors

As of 6/1/22:

Paddy3118 tasks:199
CalmoSoft tasks:128
Markhobley tasks:71
Gerard Schildberger tasks:66
Mwn3d tasks:55
Thundergnat tasks:44
Total: 1492 tasks by 307 authors

As of 27/12/22:

Paddy3118 tasks:199
CalmoSoft tasks:135
Thundergnat tasks:85
Markhobley tasks:71
Gerard Schildberger tasks:66
Mwn3d tasks:55
Total: 1592 tasks by 318 authors

--*/
