--
-- demo\rosetta\SierpinskyTriangle.exw
-- ===================================
--
with javascript_semantics
include pGUI.e

Ihandle dlg, canvas
cdCanvas cddbuffer, cdcanvas

procedure SierpinskyTriangle(integer level, atom x, atom y, atom w, atom h)
    atom w2 = w/2, w4 = w/4, h2 = h/2
    if level=1 then
        cdCanvasBegin(cddbuffer,CD_CLOSED_LINES)
        cdCanvasVertex(cddbuffer, x, y)
        cdCanvasVertex(cddbuffer, x+w2, y+h)
        cdCanvasVertex(cddbuffer, x+w, y)
        cdCanvasEnd(cddbuffer)
    else
        SierpinskyTriangle(level-1, x,    y,    w2, h2)
        SierpinskyTriangle(level-1, x+w4, y+h2, w2, h2)
        SierpinskyTriangle(level-1, x+w2, y,    w2, h2)
    end if
end procedure

integer level = 7

function redraw_cb(Ihandle /*ih*/, integer /*posx*/, /*posy*/)
    integer {w, h} = IupGetIntInt(canvas, "DRAWSIZE")
    cdCanvasActivate(cddbuffer)
    cdCanvasClear(cddbuffer)
    SierpinskyTriangle(level, w*0.05, h*0.05, w*0.9, h*0.9)
    cdCanvasFlush(cddbuffer)
    return IUP_DEFAULT
end function

function map_cb(Ihandle ih)
    cdcanvas = cdCreateCanvas(CD_IUP, ih)
    cddbuffer = cdCreateCanvas(CD_DBUFFER, cdcanvas)
    cdCanvasSetBackground(cddbuffer, CD_WHITE)
    cdCanvasSetForeground(cddbuffer, CD_GRAY)
    return IUP_DEFAULT
end function

procedure set_dlg_title()
    IupSetStrAttribute(dlg, "TITLE", "Sierpinsky Triangle (level %d)",{level})
end procedure

function key_cb(Ihandle /*ih*/, atom c)
    if c=K_ESC then return IUP_CLOSE end if
    if find(c,"+-") then
        if c='+' then
            level = min(12,level+1)
        elsif c='-' then
            level = max(1,level-1)
        end if
        set_dlg_title()
        IupRedraw(canvas)
    end if
    return IUP_CONTINUE
end function

procedure main()
    IupOpen()
    canvas = IupCanvas("RASTERSIZE=640x640")
    IupSetCallbacks(canvas, {"MAP_CB", Icallback("map_cb"),
                             "ACTION", Icallback("redraw_cb")})
    dlg = IupDialog(canvas)
    IupSetCallback(dlg, "KEY_CB", Icallback("key_cb"))
    set_dlg_title()
    IupShow(dlg)
    IupSetAttribute(canvas, "RASTERSIZE", NULL)
    if platform()!=JS then
        IupMainLoop()
        IupClose()
    end if
end procedure

main()

