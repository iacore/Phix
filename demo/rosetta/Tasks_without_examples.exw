--
-- demo\rosetta\Tasks_without_examples.exw
-- =======================================
--
--  Extract just the descriptions of tasks, without any implementions.
--  Since downloading all the pages can be very slow, this uses a cache.
--  Note that displaying text extracted from .html (as required by the
--  task specification) on a Windows console is fraught with difficulty.
--  Went a bit overboard in text_clean() before realising I really ought
--  not to bother at all, the html is much better and perfectly fine.
--
without js -- (libcurl, file i/o, peek, progress..)
constant output_html = true,
--constant output_html = false,
         include_drafts = true,
--       summary = true,
         summary = false,
--       notlang = "Phix"
--       notlang = "Go"
--       notlang = "Julia"
--       notlang = "Python"
--       notlang = "Wren"
         notlang = ""

include rosettacode_cache.e -- see [[Rosetta_Code/Count_examples#Phix]]

function extract_tasks()
    sequence tasks = get_rc_category("Programming_Tasks")
    if include_drafts then
        tasks &= get_rc_category("Draft_Programming_Tasks")
        tasks = sort(tasks)
    end if
    if length(notlang) then
        tasks = filter(tasks,"out",get_rc_category(notlang))
    end if
    if not summary then
        -- replace with contents
        atom t1 = time()+1
        for i=1 to length(tasks) do
            if time()>t1 then
                progress("processing %d/%d...\r",{i,length(tasks)})
                t1 = time()+1
            end if
            string ti = tasks[i], contents = get_rc_htm(ti)
            integer start = match(`</div>`,contents,match(`<div class="infobox"`,contents))+length(`</div>`),
                   finish = match(`<div id="toc"`,contents,start)-1
            -- ... but draft tasks with too few languages have no toc:
            if finish=-1 then finish = match(`<h2>`,contents,start)-1 end if
            -- ... and if no languages at all, use the footer:
            if finish=-1 then finish = match(`</div><div class="printfooter">`,contents,start)-1 end if
            if finish=-1 then ?9/0 end if
            contents = contents[start..finish]
            ti = substitute(ti,"_"," ")
            if not match("<h3>"&ti&"</h3>",contents)
            and not match("<b>"&ti&"</b>",contents) then
                contents = sprintf("<h3>%s</h3>\n%s",{ti,contents})
            end if
            tasks[i] = contents
            if get_key()=#1B then exit end if
        end for
    end if
    curl_cleanup()
    progress("")
    return tasks
end function

constant html_header = """
<!DOCTYPE html>
<html lang="en">
 <head>
  <meta charset="utf-8" />
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
  <title>Rosettacode Tasks without examples</title>
 </head>
 <body>
  <h2>Rosettacode Tasks without examples</h2>
  Generated %s, %d entries<br>
  <br>
""",
         html_footer = """
 </body>
</html>
"""
include regex.e -- gsub()

sequence results = extract_tasks()

if output_html then
    string filename = "Tasks_Without_Examples.html"
    integer fn = open(filename,"w")
    printf(fn,html_header,{format_timedate(date()),length(results)})
    for i=1 to length(results) do
        printf(fn,"%s<br>\n",html_clean(results[i]))
    end for
    puts(fn,html_footer)
    close(fn)
    printf(1,"%d tasks written to %s\n",{length(results), filename})
else
    for i,r in results do
        printf(1,"%s\n",text_clean(gsub("<[^>]*>",r,"")))
    end for
    printf(1,"%d tasks\n",length(results))
end if

?"done"
{} = wait_key()

