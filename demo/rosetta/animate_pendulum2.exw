--
-- demo\rosetta\animate_pendulum2.exw
-- ==================================
--
-- Author Pete Lomax, March 2017
--
-- Port of animate_pendulum.exw from arwen to pGUI
--
-- With help from lesterb, updates now in timer_cb not redraw_cb,
-- variables better named, and velocity problem sorted, July 2018.
--
include pGUI.e

Ihandle dlg, canvas, timer
cdCanvas cddbuffer, cdcanvas

constant g = 50

atom angle = PI/2,
     velocity = 0
integer w, h, len = 0

function redraw_cb(Ihandle /*ih*/, integer /*posx*/, integer /*posy*/)
    {w, h} = IupGetIntInt(canvas, "DRAWSIZE")
    cdCanvasActivate(cddbuffer)
    cdCanvasClear(cddbuffer)
    -- new suspension point:
    integer sX = floor(w/2)
    integer sY = floor(h/8)
    -- repaint:
    integer eX = floor(len*sin(angle)+sX)
    integer eY = floor(len*cos(angle)+sY)
    cdCanvasSetForeground(cddbuffer, CD_DARK_GREY)
    cdCanvasLine(cddbuffer, sX, h-sY, eX, h-eY)
    cdCanvasSetForeground(cddbuffer, CD_BLACK)
    cdCanvasSector(cddbuffer, eX, h-eY, 35, 35, 0, 360)
    cdCanvasFlush(cddbuffer)
    return IUP_DEFAULT
end function

function timer_cb(Ihandle /*ih*/)
    integer newlen = floor(w/2)-30
    if newlen!=len then
        len = newlen
        if angle>=PI/2
        or angle<=-PI/2 then
            velocity = 0    -- (avoid getting nan)
        else
            velocity = sqrt(2*g*len*(cos(angle)))*sign(velocity)
        end if
    end if
    atom dt = 0.2/w
    atom delta = -len*sin(angle)*g
    velocity += dt*delta
    angle += dt*velocity
    IupUpdate(canvas)
    return IUP_IGNORE
end function

function map_cb(Ihandle ih)
    cdcanvas = cdCreateCanvas(CD_IUP, ih)
    cddbuffer = cdCreateCanvas(CD_DBUFFER, cdcanvas)
    cdCanvasSetBackground(cddbuffer, CD_GREY)
    return IUP_DEFAULT
end function

function esc_close(Ihandle /*ih*/, atom c)
    if c=K_ESC then return IUP_CLOSE end if
    return IUP_CONTINUE
end function

procedure main()
    IupOpen()

    canvas = IupCanvas(NULL)
    IupSetAttribute(canvas, "RASTERSIZE", "640x380")
    IupSetCallback(canvas, "MAP_CB", Icallback("map_cb"))
    IupSetCallback(canvas, "ACTION", Icallback("redraw_cb"))

    timer = IupTimer(Icallback("timer_cb"), 20)

    dlg = IupDialog(canvas)
    IupSetAttribute(dlg, "TITLE", "Animated Pendulum")
    IupSetCallback(dlg, "K_ANY", Icallback("esc_close"))

    IupShow(dlg)
    IupSetAttribute(canvas, "RASTERSIZE", NULL)
    IupMainLoop()
    IupClose()
end procedure

main()

