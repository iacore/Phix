--
-- tagsort.exw
-- ===========
--
--  A tag sort is where you sort an array of indexes rather than the data itself.
--
--  Often it is very useful to leave the data completely unaltered, either because
--  more than one thing might be referencing that data, or (as shown below) where
--  the sort could otherwise be tricky as it would involve moving/swapping elements
--  from multiple independent arrays in tandem.
--
--  Despite any apparent simplicity, this is a very important and useful technique, 
--   and you should take extra special effort to fully understand and remember it.
--

sequence Names = {"C. C. Catch", "Army of Lovers", "Boney M.", "Dschinghis Khan"}
sequence Years = { 1985,          1987,             1975,       1979            }

function by_year(integer i, integer j)
integer res
    res = compare(Years[i],Years[j])
    if res=0 then
        res = compare(Names[i],Names[j])
    end if
    return res
end function
sequence yeartags = custom_sort(routine_id("by_year"),tagset(length(Years)))

function by_name(integer i, integer j)
integer res
    res = compare(Names[i],Names[j])
    if res=0 then
        res = compare(Years[i],Years[j])
    end if
    return res
end function
sequence nametags = custom_sort(routine_id("by_name"),tagset(length(Names)))

integer ti
    for i=1 to length(yeartags) do
        ti = yeartags[i]
        printf(1,"Year: %d, Name: %s\n",{Years[ti],Names[ti]})
    end for
    for i=1 to length(nametags) do
        ti = nametags[i]
        printf(1,"Name: %s, Year: %d\n",{Names[ti],Years[ti]})
    end for
if getc(0) then end if
