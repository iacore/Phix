-- Win32Dib demo: demonstrates the scale-function

--without warning
include w32dgeom.ew  -- only the core- and geometric manipulation-library are necessary

object dib, scale_dib
dib = loadDib("demo.bmp")
if not sequence(dib) then
    VOID = message_box("Can't load bitmap 'demo.bmp'.", "Error", MB_ICONERROR)
    abort(0)
end if
scale_dib = 0

constant win = create(Window, "Win32Dib bitmap scale demo -- resize the window to see the scaling", 0, Default, Default, 600, 400, 0)
VOID = setErasePolicy(win, w32True)

procedure winPaint(integer self, integer event, sequence parms)
    if self or event or sequence(parms) then end if -- suppress warnings
    if sequence(scale_dib) then
	drawDib(win, scale_dib, 0, 0, 0, 0, scale_dib[DibWidth] - 1, scale_dib[DibHeight] - 1)
    end if
end procedure
setHandler(win, w32HPaint, routine_id("winPaint"))

procedure winResize(integer self, integer event, sequence parms)
sequence rect
    if self or event or sequence(parms) then end if -- suppress warnings
    rect = getClientRect(win)
    if sequence(scale_dib) then killDib(scale_dib) end if
    scale_dib = scaleDib(dib, rect[3], rect[4], False)
    repaintWindow(win)
end procedure
setHandler(win, w32HResize, routine_id("winResize"))

WinMain(win, Normal)
killDib(dib)
if sequence(scale_dib) then killDib(scale_dib) end if
