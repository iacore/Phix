--
-- demo\xpGUI\gTimer.exw
-- =====================
--
--  There is a deliberately slightly simpler version of this in the docs.
--
--  Includes one method to tick once per interval, or as close as possible.
--  (At least under Windows, some runs get no drifts at all, whereas some
--   get a drift every other tick, presumably cos "next not landable"...)
--
--requires(64)
with javascript_semantics
include xpGUI.e
--gUseGTK()
constant title = "gTimer",
         bCorrectDrift = false,
--       bCorrectDrift = true,
         interval = iff(bCorrectDrift?1000:100)
integer next = interval
gdx timer, dlg

procedure action(gdx /*timer*/)
    if bCorrectDrift then
        integer drift = floor(time()*1000)-next
        string sdrift = iff(drift?sprintf("(%d)",drift):"")
        gSetAttribute(dlg,"TITLE","%s:%.1f%s",{title,time(),sdrift})
        gSetAttribute(timer,"MSECS",max(1000-drift,10))
        next += interval
    else
        gSetAttribute(dlg,"TITLE","%s:%.1f",{title,time()})
    end if
end procedure

integer w = iff(bCorrectDrift?260:240), h=80
timer = gTimer(action,interval,true)
dlg = gDialog(NULL,title,`SIZE=%dx%d`,{w,h})
gShow(dlg)
gMainLoop()
